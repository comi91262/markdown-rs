// A Unicode whitespace character is any code point in the Unicode Zs general category, or a tab (U+0009),
//  carriage return (U+000D), newline (U+000A), or form feed (U+000C).
// 
// Unicode Zs general category.
//
// U+0020 SPACE
// U+00A0 NO-BREAK SPACE
// U+1680 OGHAM SPACE MARK
// U+2000 EN QUAD
// U+2001 EM QUAD
// U+2002 EN SPACE
// U+2003 EM SPACE
// U+2004 THREE-PER-EM SPACE
// U+2005 FOUR-PER-EM SPACE
// U+2006 SIX-PER-EM SPACE
// U+2007 FIGURE SPAC
// U+2008 PUNCTUATION SPACE
// U+2009 THIN SPACE
// U+200A HAIR SPACE
// U+202F NARROW NO-BREAK SPACE
// U+205F MEDIUM MATHEMATICAL SPACE
// U+3000 IDEOGRAPHIC SPACE
//

// unicode_whitespace_character = { "\u{0020}" | "\u{00A0}" | "\u{1680}" | '\u{2000}'..'\u{200A}' | "\u{202F}" | "\u{205F}" 
//                               | "\u{0009}" | "\u{000D}" | "\u{000A}" | "\u{000C}" }
//
//u nicode_whitespace = { unicode_whitespace_character+ }
//
space = _{ "\u{0020}" }
tab = _{ "\u{0009}" }


ascii_punctuation_character = { "!" | "\"" | "#" | "$" | "%" | "'"
                              | "(" | ")" | "*" | "+" | "," | "-"
                              | "." | "/" | ":" | ";" | "<" | "="
                              | ">" | "?" | "@" | "[" | "\\" | "]"
                              | "^" | "_" | "`" | "{" | "|" | "}" | "~" }


// TODO: A punctuation character is an ASCII punctuation character or anything in the general Unicode categories Pc, Pd, Pe, Pf, Pi, Po, or Ps.
// TODO: A line containing no characters, or a line containing only spaces (U+0020) or tabs (U+0009), is called a blank line.
// TODO: A non-whitespace character is any character that is not a whitespace character.


no_token_text = _{ (!newline ~ !carriage_return ~ character)+ }
text = { (!newline ~ !carriage_return ~ character)+ }
                            
sharp = _{ "#" }
equal = _{ "=" }
astar = _{ "*" }
hyphen = _{ "-" }
under = _{ "_" }
character = _{ any } 

indented_code_block = {
    (space{4} | space* ~ tab ~ space*) ~ text
}

atx_heading1 = { sharp ~ space+ ~ text }
atx_heading2 = { sharp{2} ~ space+ ~ text }
atx_heading3 = { sharp{3} ~ space+ ~ text }
atx_heading4 = { sharp{4} ~ space+ ~ text }
atx_heading5 = { sharp{5} ~ space+ ~ text }
atx_heading6 = { sharp{6} ~ space+ ~ text }

atx_headings = _{ 
    space{0, 3} ~ ( atx_heading1 | atx_heading2 | atx_heading3 | atx_heading4 | atx_heading5 | atx_heading6)
}

setext_heading_underline1 = {
    equal+
}

setext_heading_underline2 = {
    hyphen+
}


//                none |  paragraph|
//  --------------------------------
//  -     -> paragraph |    setext |
//  --    -> paragraph |    setext |
//  _ _   -> list      |      list |
//  ---   -> thematic  |    setext |
//  - --  -> thematic  |  thematic |
//  ----  -> thematic  |    setext |
//  --- - -> thematic  |  thematic |
//  ----a -> paragraph | paragraph |
//  --------------------------------
//
setext_heading_underlines = _{
    setext_heading_underline1
  | setext_heading_underline2
}


thematic_break =
{
    space* ~ astar ~ space* ~ astar ~ space* ~ astar ~ (space* ~ astar)* ~ space*
  | space* ~ hyphen ~ space* ~ hyphen ~ space* ~ hyphen ~ (space* ~ hyphen)* ~ space*
  | space* ~ under ~ space* ~ under ~ space* ~ under ~ (space* ~ under)* ~ space*
}



fenced_marker1 = _{ "```" }
fenced_marker2 = _{ "~~~" }
fenced_text1 = { (!"`" ~ character)+ }
fenced_text2 = { (!"~" ~ character)+ }

fenced_code_block = {
    space{0, 3} ~ ( fenced_marker1 ~ line_ending ~ fenced_text1 ~ fenced_marker1 
                  | fenced_marker2 ~ line_ending ~ fenced_text2 ~ fenced_marker2 )
}



newline = _{ "\u{000A}" }
carriage_return = _{ "\u{000D}" }
paragraph = { no_token_text }

quote_marker = _{ ">" }

block_quote = {
  space{0, 3} ~ quote_marker ~ space{0, 1} ~ line 
}

bullet_list_marker = _{ "*" | "+" | "-" }

bullet_list_items_space0 = _{
   bullet_list_marker ~ (
      (space{1} ~ !space ~ text ~ (line_ending ~ (space{2} ~ line | break_line))*) 
    | (space{2} ~ !space ~ text ~ (line_ending ~ (space{3} ~ line | break_line))*)
    | (space{3} ~ !space ~ text ~ (line_ending ~ (space{4} ~ line | break_line))*)
    | (space{4} ~ !space ~ text ~ (line_ending ~ (space{5} ~ line | break_line))*))
}
bullet_list_items_space1 = _{
   space{1} ~ bullet_list_marker ~ (
      (space{1} ~ !space ~ text ~ (line_ending ~ (space{3} ~ line | break_line))*) 
    | (space{2} ~ !space ~ text ~ (line_ending ~ (space{4} ~ line | break_line))*)
    | (space{3} ~ !space ~ text ~ (line_ending ~ (space{5} ~ line | break_line))*)
    | (space{4} ~ !space ~ text ~ (line_ending ~ (space{6} ~ line | break_line))*))
}
bullet_list_items_space2 = _{
  space{2} ~ bullet_list_marker ~ (
      (space{1} ~ !space ~ text ~ (line_ending ~ (space{4} ~ line | break_line))*) 
    | (space{2} ~ !space ~ text ~ (line_ending ~ (space{5} ~ line | break_line))*)
    | (space{3} ~ !space ~ text ~ (line_ending ~ (space{6} ~ line | break_line))*)
    | (space{4} ~ !space ~ text ~ (line_ending ~ (space{7} ~ line | break_line))*))
}
bullet_list_items_space3 = _{
  space{3} ~ bullet_list_marker ~ (
      (space{1} ~ !space ~ text ~ (line_ending ~ (space{5} ~ line | break_line))*) 
    | (space{2} ~ !space ~ text ~ (line_ending ~ (space{6} ~ line | break_line))*)
    | (space{3} ~ !space ~ text ~ (line_ending ~ (space{7} ~ line | break_line))*)
    | (space{4} ~ !space ~ text ~ (line_ending ~ (space{8} ~ line | break_line))*))
}

bullet_list_items = {
  bullet_list_items_space0 
  | bullet_list_items_space1
  | bullet_list_items_space2
  | bullet_list_items_space3
} 

digit = _{ '0'..'9' }  // TODO  modify that 0 not starts
ordered_list_marker = _{ digit{1, 10} ~ ("." | ")") }

ordered_list_items = {
  space{0, 3} ~ ordered_list_marker ~ 
  ((space{1} ~ !space ~ paragraph 
   ~ (line_ending ~ (space{3} ~ line | break_line))*) 
  | (space{2} ~ !space ~ paragraph
   ~ (line_ending ~ (space{4} ~ line | break_line))*)
  | (space{3} ~ !space ~ paragraph
   ~ (line_ending ~ (space{5} ~ line | break_line))*)
  | (space{4} ~ !space ~ paragraph
   ~ (line_ending ~ (space{6} ~ line | break_line))*)) 
}

break_line = {
  ("\u{0020}" | "\u{0009}")* ~ &line_ending
}

line = _{
    indented_code_block     
  | fenced_code_block
  | block_quote
  | setext_heading_underlines ~ !text
  | atx_headings
  | thematic_break ~ !text
  | bullet_list_items
  | ordered_list_items
  | break_line
  | paragraph
}

line_ending = _{ newline | carriage_return | newline ~ carriage_return }
document = _{ soi ~ (line ~ line_ending)+ ~ eoi }

        



